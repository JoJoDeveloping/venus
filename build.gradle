group 'venus'
version '0.1.0'

buildscript {
    ext.kotlin_version = '1.3.61'
    ext.dokka_version = '0.10.1'
    ext.ktlint_version = '0.27.0'
    ext.serialization_version = '0.20.0'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

apply plugin: 'kotlin2js'
apply plugin: 'kotlin-dce-js'
apply plugin: 'kotlinx-serialization'
apply plugin: 'org.jetbrains.dokka'

runDceKotlinJs.keep 'venus.Driver.run', 'venusbackend.assembler.Linter', 'venus.api.API', "kotlin.Number"

runDceKotlinJs.dceOptions.devMode = true
runDceTestKotlinJs.dceOptions.devMode = true

configurations {
    ktlint
}

repositories {
    mavenCentral()
    jcenter()
}

compileKotlin2Js {
    kotlinOptions.sourceMap = true
    kotlinOptions.sourceMapEmbedSources = "always"
    kotlinOptions.moduleKind = "commonjs"
    // remaining configuration options
}

task ktlint(type: JavaExec) {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "--verbose", "!src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec) {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "!src/**/*.kt"
}

dokka {
    outputFormat = 'html'
    outputDirectory = "doc"
}

dependencies {
    ktlint "com.github.shyiko:ktlint:$ktlint_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$serialization_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
}